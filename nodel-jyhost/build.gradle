plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'eclipse'
}

repositories {
    mavenCentral()
}

def gitBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
def gitId = 'git rev-parse HEAD'.execute().text.trim()
def gitRev = 'git rev-list --count HEAD'.execute().text.trim()

def branch = gitBranch
if (branch.equals("master"))
    branch = "dev"

def rev = gitRev
// strip out non-alphanumeric (e.g. the '+' in '1234+')
rev = rev.replaceAll(/[^a-zA-Z0-9]/, '')

def buildSummary = gitId

application {
    mainClass = 'org.nodel.jyhost.Launch'
}

jar {
    from "$buildDir/output"
    archiveBaseName = 'nodel-jyhost'
    manifest {
        attributes 'Implementation-Title': 'Nodel JyHost',
                'Implementation-Version': version
    }
}

tasks.register('fatJar', Jar) {
    manifest {
        attributes 'Implementation-Title': 'Nodel JyHost',
                'Implementation-Version': version,
                'Main-Class': application.mainClass
    }
    archiveAppendix = branch
    archiveBaseName = 'nodelhost'
    archiveClassifier = 'rev' + rev
    destinationDirectory = new File(buildDir, 'distributions/standalone')
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('unversioned', Copy) {
    dependsOn 'fatJar'
    from(new File(buildDir, 'distributions/standalone')) {
        rename '.*($version)*', 'nodelhost.jar'
    }

    into new File(buildDir, 'distributions/standalone')
}

tasks.build {
    finalizedBy 'fatJar'
}

dependencies {
    implementation project(':nodel-webui-js')
    implementation project(':nodel-framework')
    implementation 'commons-daemon:commons-daemon:1.0.15'
    implementation 'org.slf4j:slf4j-api:1.7.10'
    implementation 'org.python:jython-standalone:2.5.4-rc1'

    // for the Nodel HTTP client:
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.5'

    // convenience dependency: required *only* for 'recipe sync' node
    implementation 'org.eclipse.jgit:org.eclipse.jgit:3.7.1.+'

    // convenience dependency: required for new SNMP-based nodes
    implementation('org.snmp4j:snmp4j:2.5.8') {
        exclude group:'log4j', module:'log4j' // (brings one extra dependency: log4j; can explicity exclude it because it's optional at runtime)
    }

    // convenience dependency: JWTs (JSON Web Tokens), see https://github.com/museumsvictoria/nodel/discussions/297
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-orgjson:0.11.5'

    // Add joda-time dependency
    implementation 'joda-time:joda-time:2.10.14'
}