import org.apache.tools.ant.filters.ReplaceTokens
import org.joda.time.DateTime

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'joda-time:joda-time:2.10.10'
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'com.github.node-gradle.node' version '7.0.2'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'joda-time:joda-time:2.10.10'
    implementation 'org.joda:joda-convert:2.2.1'
}

static def getGitInfo() {
    return [
            branch : 'git rev-parse --abbrev-ref HEAD'.execute().text.trim(),
            id     : 'git rev-parse HEAD'.execute().text.trim(),
            rev    : 'git rev-list --count HEAD'.execute().text.trim(),
            origin : 'git config --get remote.origin.url'.execute().text.trim()
    ]
}

def buildSummary = gitInfo.id
def hostname = InetAddress.getLocalHost().getHostName().toUpperCase()
def now = DateTime.now().toString()
def rev = gitInfo.rev.replaceAll(/[^a-zA-Z0-9]/, '')
def branch = gitInfo.branch

version = gitInfo.branch != 'stable' ? "${project.version}-${branch}_r${rev}" : project.version

node {
    version = '12.19.0'
    download = true

    workDir = file("${project.buildDir}/nodejs")
    nodeProjectDir = file("${project.projectDir}")
}

class FileChecker {
    def nodeModulesDir
    def srcDir
    def packageJson
    def packageLockJson
    def gruntFile

    FileChecker(Project project) {
        nodeModulesDir = project.file("${project.projectDir}/node_modules")
        srcDir = project.file('src')
        packageJson = project.file('package.json')
        packageLockJson = project.file('package-lock.json')
        gruntFile = project.file('Gruntfile.js')
    }

    def checkFilesChanged() {
        if (!nodeModulesDir.exists()) {
            return true
        } else {
            def srcDirModified = srcDir.exists() && srcDir.lastModified() > nodeModulesDir.lastModified()
            def packageJsonModified = packageJson.exists() && packageJson.lastModified() > nodeModulesDir.lastModified()
            def packageLockJsonModified = packageLockJson.exists() && packageLockJson.lastModified() > nodeModulesDir.lastModified()
            def gruntFileModified = gruntFile.exists() && gruntFile.lastModified() > nodeModulesDir.lastModified()

            def filesChanged = srcDirModified || packageJsonModified || packageLockJsonModified || gruntFileModified

            return filesChanged
        }
    }
}

def fileChecker = new FileChecker(project)

tasks.register('gruntRun', NpxTask) {
    def shouldInstall = fileChecker.checkFilesChanged()

    if (shouldInstall) {
        dependsOn clean, npmInstall
    }

    command = "grunt"
    args = ["build"]
    inputs.property('filesChanged', shouldInstall)
    inputs.dir("node_modules")
    outputs.dir("build/grunt")

    onlyIf { shouldInstall }

    doLast {
        if (shouldInstall) {
            logger.info('Running Grunt build task as files have changed or node_modules is missing.')
        } else {
            logger.info('Skipping Grunt build task as no files have changed and node_modules is present.')
        }
    }
}

tasks.named('clean', Delete) {
    delete "${projectDir}/node_modules/"
    delete "${projectDir}/build/grunt/"
}

tasks.register('copyContent', Copy) {
    dependsOn gruntRun
    from file("${project.buildDir}/grunt")
    into file("${project.buildDir}/www-content_stage")
    exclude 'build.json'
}

tasks.register('filterContentTemplates', Copy) {
    from 'src'
    into file("${project.buildDir}/www-content_stage")
    include 'build.json'
    filter(ReplaceTokens, tokens: [
            buildSummary: (project.name + "-" + project.version + " " + buildSummary + " (" + hostname + ") " + now).toString(),
            buildOrigin : gitInfo.origin.toString(),
            buildProject: project.name.toString(),
            buildBranch : branch.toString(),
            buildVersion: project.version.toString(),
            buildId     : buildSummary.toString(),
            buildRev    : rev.toString(),
            buildHost   : hostname.toString(),
            buildDate   : now.toString()
    ])
}

tasks.register('zipContentInterface', Zip) {
    dependsOn copyContent, filterContentTemplates
    from file("${project.buildDir}/www-content_stage")
    archiveName 'content.zip'
    destinationDirectory = file("${project.buildDir}/www-content/org/nodel/host")
}

tasks.register('copyBuildInfo', Copy) {
    dependsOn copyContent, filterContentTemplates
    from file("${project.buildDir}/www-content_stage")
    include 'build.json'
    into file("${project.buildDir}/www-content/org/nodel")
}

sourceSets {
    main {
        resources.srcDirs = [file("${project.buildDir}/www-content")]
    }
}

tasks.named('compileJava') {
    dependsOn zipContentInterface, copyBuildInfo
}

tasks.named('processResources') {
    dependsOn zipContentInterface, copyBuildInfo
}

dependencies {
    implementation 'joda-time:joda-time:2.10.10'
    implementation 'org.joda:joda-convert:2.2.1'
}